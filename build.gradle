apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

buildscript {
    repositories {
        mavenCentral()
    }
}

jacoco {
    toolVersion = "0.6.5.201403032054"
}

jacocoTestReport {
    group = "reporting"
    description = "Generate Jacoco coverage reports after running tests."
    additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
}

configurations {
    jaxDoclet
    compile
    runtime
}


sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

group = 'fm.audiobox'
version = '1.0.0'

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'audiobox-jlib'
                packaging 'jar'
                description 'Next generation Java library for AudioBox API'
                url 'https://github.com/icoretech/audiobox-jlib'

                scm {
                    url 'scm:git@github.com:icoretech/audiobox-jlib.git'
                    connection 'scm:git@github.com:icoretech/audiobox-jlib.git'
                    developerConnection 'scm:git@github.com:icoretech/audiobox-jlib.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'keytwo'
                        name 'Valerio Chiodino'
                    }
                }
            }
        }
    }
}

idea {

    module {

        //if you love browsing Javadoc
        downloadJavadoc = true

        //and hate reading sources :)
        downloadSources = true
    }

    project {
        jdkName = '1.7'

        languageLevel = '1.7'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // Logging
    compile     'org.slf4j:slf4j-api:1.7.+'

    // Configuration
    compile     group: 'com.typesafe', name: 'config', version: '1.2.+'

    // Http
    compile     'com.google.http-client:google-http-client:1.18.0-rc'
    compile     'com.google.oauth-client:google-oauth-client:1.18.0-rc'

    // Common utils
    compile     'commons-validator:commons-validator:+'
    compile     'org.apache.commons:commons-lang3:+'
    compile     'de.danielbechler:java-object-diff:0.13+'


    // Testing
    testCompile 'junit:junit:4.11+'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'com.google.http-client:google-http-client-jackson2:1.18.0-rc'

    // Test logging
    testCompile 'ch.qos.logback:logback-classic:1.+'
    testCompile 'org.slf4j:jul-to-slf4j:1.7.+'

    // Cache
    testCompile  'com.integralblue:httpresponsecache:1.3+'

    // Utils
    testCompile  'org.apache.commons:commons-io:1.3+'
    testCompile  'nl.jqno.equalsverifier:equalsverifier:1.4+'


    // JavaDoc
    jaxDoclet("com.google.doclava:doclava:1.+")
}

task generateDoclava(type: Javadoc) {

    description = "Build the Doclava Javadocs"
    source = sourceSets.main.allJava
    options.docletpath = configurations.jaxDoclet.files.asType(List)
    maxMemory = "1024m"
    classpath = configurations.compile
    destinationDir = file("${project.docsDir}/apidocs")


    title = ""

    options {
        doclet = "com.google.doclava.Doclava"
        bootClasspath new File(System.getenv('JAVA_HOME') + "/jre/lib/rt.jar")

        addStringOption("quiet")
        addStringOption("hdf", "project.name ${project.name}")
        addStringOption("stubs", "${project.docsDir}/stubs")
        addStringOption("apixml", "${project.docsDir}/apidocs/api.xml")
        addStringOption("stubs", "${project.docsDir}/apidocs")
        addStringOption("templatedir", "${rootDir}/src/javadoc")
    }
}
